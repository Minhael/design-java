plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
}

ext {
    release = 'http://ne.xus/repository/maven-releases/'
    snapshots = 'http://ne.xus/repository/maven-snapshots/'
    versionTag = project.file('.tags').absolutePath
}

group "me.minhael"

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
    }

    test {
        useJUnitPlatform()
    }

    task sourcesJar(type: Jar, dependsOn: project.classes) {
        from sourceSets.main.allSource
        archiveClassifier.set("sources")
    }

    publishing {
        repositories {
            maven {
                url "${System.env.is_release ? release : snapshots}"
                credentials {
                    username "${System.env.repo_user}"
                    password "${System.env.repo_password}"
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                version = "${new File(versionTag).text.trim()}${System.env.is_release ? "" : "-SNAPSHOT"}"

                from components.java
                artifact sourcesJar
            }
        }
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group rootProject.group
    version rootProject.version

    dependencies {
        api rootProject
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    //  Logging
    api 'org.slf4j:slf4j-api:1.7.30'
}